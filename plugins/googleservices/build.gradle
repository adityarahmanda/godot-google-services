plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode 1
        versionName "1.0"
    }

    namespace = "adityarahmanda.googleservices"

    // Used to customize the name of generated AAR file.
    libraryVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            output.outputFileName = "GoogleServices.${variant.name}.aar"
        }
    }

}

dependencies {
    compileOnly libraries.godotAndroidLib
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlinVersion"

    implementation platform('com.google.firebase:firebase-bom:32.3.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation "com.google.android.gms:play-services-ads:22.6.0"
    implementation "com.google.android.gms:play-services-games-v2:20.0.0"
    implementation 'com.google.android.ump:user-messaging-platform:2.1.0'
}

tasks.register('copyDebugAARToDemoAddons', Copy) {
    from 'build/outputs/aar'
    include 'GoogleServices.debug.aar'
    into 'addons/googleservices/bin/debug'
}

tasks.register('copyReleaseAARToDemoAddons', Copy) {
    from 'build/outputs/aar'
    include 'GoogleServices.release.aar'
    into 'addons/googleservices/bin/release'
}


assemble.finalizedBy(copyDebugAARToDemoAddons)
assemble.finalizedBy(copyReleaseAARToDemoAddons)
